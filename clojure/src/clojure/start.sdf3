module clojure/start

context-free start-symbols

  CStart

context-free sorts

  CStart CExpr

context-free syntax

  CStart.CProgram = es:CExpr* {layout(align-list es)}
  
  CExpr.CFn = <(fn [<CID>] <CExpr>)>
  CExpr.CFnCall = <(<CExpr> <CExpr>)>
  CExpr.CLet = <(let [<CID> <CExpr>] <CExpr>)>
  CExpr.CVar = CID
  CExpr.CPrintLn = <(println <CExpr>)>
  CExpr.CString = STRING
  
  CExpr.CTrue = "true"
  CExpr.CFalse = "false"
  CExpr.CIf = <(if <CExpr> <CExpr> <CExpr>)>
  

lexical sorts STRING STRING_CHAR CID
  
lexical syntax

  LAYOUT = [\ \n\t\v\f\r]

lexical syntax
  STRING           = "\"" STRING_CHAR* "\""
  STRING_CHAR      = [\32-\126] / [\\\"]
  STRING_CHAR      = "\\n" | "\\\\" | "\\\"" | "\\t"
  
  CID = [a-zA-Z\_] [a-zA-Z0-9\_]*
  CID = "fn" {reject}
  CID = "let" {reject}
  CID = "println" {reject}
  CID = "true" {reject}
  CID = "false" {reject}
  CID = "if" {reject}
  
lexical restrictions
  CID -/- [a-zA-Z0-9\_]
  "fn" -/- [a-zA-Z0-9\_]
  "let" -/- [a-zA-Z0-9\_]
  "println" -/- [a-zA-Z0-9\_]
  "true" -/- [a-zA-Z0-9\_]
  "false" -/- [a-zA-Z0-9\_]
  "if" -/- [a-zA-Z0-9\_]
  

context-free restrictions

  LAYOUT? -/- [\ \n\t\v\f\r]
