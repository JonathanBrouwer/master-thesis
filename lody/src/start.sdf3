module start

context-free start-symbols

  Start

context-free sorts

  Start Expr ID DataConstructor DataParam

context-free syntax

  Start.Program = Expr
  
  Expr.Let = [let [ID] = [Expr]; [Expr]]
  Expr.Type = "Type"
  Expr.Var = ID
  Expr.FnType = ID ":" Expr "->" Expr {right}
  Expr.FnConstruct = "\\" ID ":" Expr "." Expr
  Expr.FnDestruct = Expr Expr {left}
  
  Expr.Postulate = [postulate [ID]: [Expr]; [Expr]]
  Expr.TypeAssert = Expr ":" Expr
  
  Expr.BoolTrue = "true"
  Expr.BoolFalse = "false"
  Expr.BoolType = "Bool"
  Expr.BoolIf = "if" Expr "then" Expr "else" Expr "end"
  
  Expr.DataTypeDecl = "data" ID DataParam* "=" DataParam* {DataConstructor ","}* ";" Expr
  DataConstructor.Constructor = ID ":" Expr
  DataParam.Param = "(" ID ":" Expr ")"

  Expr.DataElim = "elim" ID
  
  Expr = "(" Expr ")" {bracket}
  
  ID.Syn = RID
  
context-free priorities
  {Expr.Type Expr.BoolTrue Expr.BoolFalse Expr.BoolType Expr.BoolIf} > Expr.Var > Expr.FnDestruct > Expr.TypeAssert > Expr.FnType > Expr.FnConstruct > {Expr.Let Expr.Postulate Expr.DataTypeDecl DataConstructor.Constructor}
  
context-free restrictions

  LAYOUT? -/- [\ \n\t\v\f\r]
  
lexical sorts
  RID

lexical syntax

  LAYOUT = [\ \n\t\v\f\r]
  
  RID = [a-zA-Z\_] [a-zA-Z0-9\_]*
  RID = "Type" {reject}
  RID = "let" {reject}
  RID = "true" {reject}
  RID = "false" {reject}
  RID = "Bool" {reject}
  RID = "if" {reject}
  RID = "then" {reject}
  RID = "else" {reject}
  RID = "end" {reject}
  RID = "postulate" {reject}
  RID = "elim" {reject}
  
lexical restrictions

  RID -/- [a-zA-Z0-9\_]


