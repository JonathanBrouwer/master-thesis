\chapter{Term Inference}

Inference is an important feature of dependent programming languages, that allows redundant parts of programs to be left out. For example, it allows you to infer the arguments of a function, if they can be inferred by the arguments that follow, like here where the type of the argument can be inferred, since we pass it \verb|true| which is a boolean:
\begin{lstlisting}
let id = (\T : Type. \x: T. x);
id _ true
\end{lstlisting}

We call it \emph{term inference} rather than \emph{type inference}, because we can infer values other than types. For example, it can infer that the \verb|_| in this example must be \verb|true|:
\begin{lstlisting}
postulate f: Bool -> Type;
postulate g: f true -> Type;
\x: f _. g x
\end{lstlisting}

\section{Different algorithms for inference}
\label{strength-inference}

There are a lot of different algorithms for inference\cite{typeinference}, some algorithms can solve more inferences than others. However, we would like to avoid implementing an algorithm at all, instead using Statix's built-in first-order unification to do the type inference for us. Implementing an inference algorithm in Statix is theoretically possible but this would be a lot of ugly code, and the goal is to use Statix in a way that is clean and declarative, not to do optimal inference.

The inference that we will build is a very simple unification system, where if at any point during type checking we assert that $\beq{e_1}{e_2}$ and either $e_1$ or $e_2$ is a free variable, we set the free variable to be equal to the other variable. This process is called \emph{first-order unification}.

There are some situations in which this approach fails, but in most real-world scenarios it works perfectly. For example, it can infer both programs in the introduction of this chapter, but it fails to infer the following program:
\begin{lstlisting}
let f = _;
\x : Type.
\g: (_: (f x) -> Bool).
g true
\end{lstlisting}
We know that \verb|f| is a function from \verb|Type -> Type|, but it fails to infer the value of \verb|f|. Because of the way that \verb|g| is used, the type checker asserts that $\beq{f x}{Bool}$. Since x is declared as a function argument and it is completely free, this means that for any \verb|x|, \verb|f x = Bool|. But the rule above is not powerful enough to derive all of this, so it fails.

\section{Using Statix's first-order unification for inference}
\label{statix-inference}

First, we introduce a new constructor, \verb|Infer : Expr -> Expr|. This will be used to store metavariables which we want to infer. This is a hack we use, ideally we would like to query statix to see if a metavariable is initialized, but as discussed in section \ref{strength-inference} this is not possible. 

The constructor is introduced when we encounter a \verb|_| variable, a sign that something needs to be inferred. When type-checking this, we create an infer constructor. The constraint at the bottom cannot be solved immediately, since the \verb|q| variable is unbound, but if \verb|q| is bound later the constraint will be evaluated.

\begin{lstlisting}
typeOfExpr_(s, Var(Syn("_"))) = (Infer(q), qt) :-
	(_, qt) == typeOfExpr(sEmpty(), q).
\end{lstlisting}
When an \verb|Infer| needs to be type-checked, the logic is very similar, except that we don't need to generate a new metavariable:

\begin{lstlisting}
typeOfExpr_(s, Infer(q)) = (Infer(q), t) :-
	typeOfExpr_(s, q) == (_, t).
\end{lstlisting}
When we encounter a \verb|Infer| in \verb|betaReduceHead|, we keep it intact, because we still want to know that it is a \verb|Infer| in the \verb|expectBetaEq| rule. 

The \verb|expectBetaEq| is where it gets interesting. We have a rule for each constructor in the language. For simple cases like \verb|BoolTrue|, we assert that \verb|e2| must be equal.
\begin{lstlisting}
expectBetaEq_((s1, e1@BoolTrue()), (_, Infer(e2))) :- e1 == e2.
\end{lstlisting}
For more complicated constructors such as \verb|FnType|, we generate new metavariables for the subexpressions, and assert they it must be beta-equal to the provided ones:
\begin{lstlisting}
expectBetaEq_(
	(s1, e1@FnType(arg_name1, arg_type1, body1)), 
	(_, Infer(e2))) :- {arg_type2 body2}
  e2 == FnType(arg_name1, Infer(arg_type2), Infer(body2)),
  expectBetaEq_((s1, e1), (sEmpty(), e2)).
\end{lstlisting}

Finally, there is a case for if we encounter two \verb|Infer|s. Ideally, we would look at whether one infer is instantiated, and apply the rules above. However, this is not possible since we cannot do such queries in Statix. Instead, we say that they must be exactly equal and we hope for the best. There are situations where this rule fails (where e1 and e2 are beta-equal but not identical), but this works well enough for practical use.
\begin{lstlisting}
expectBetaEq_((_, Infer(e1)), (_, Infer(e2))) :-
    e1 == e2.
\end{lstlisting}