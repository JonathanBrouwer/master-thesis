module start

context-free start-symbols

  Start

context-free sorts

  Start Expr ID DataConstructor DataParam

context-free syntax

  Start.Program = Expr
  
  Expr.Let = [let [ID] = [Expr]; [Expr]]
  Expr.TypeLit = "Type"
  Expr.TypeLitInt = "Type" INT
  Expr.Var = ID
  Expr.AnonFnType = Expr "->" Expr {right}
  Expr.FnType = ID ":" Expr "->" Expr {right}
  Expr.FnConstruct = "\\" ID ":" Expr "." Expr
  Expr.FnDestruct = Expr Expr {left}
  
  Expr.Postulate = [postulate [ID]: [Expr]; [Expr]]
  Expr.TypeAssert = Expr "::" Expr
  
  Expr.BoolTrue = "true"
  Expr.BoolFalse = "false"
  Expr.BoolType = "Bool"
  Expr.BoolIf = "if" Expr "then" Expr "else" Expr "end"
  
  Expr.DataTypeDecl = "data" ID DataParam* ":" DataParam* "->" Expr "where" {DataConstructor ","}* ";" Expr
 
  DataConstructor.Constructor = ID ":" Expr
  DataParam.Param = "(" ID ":" Expr ")"

  Expr.DataElimLit = "elim" ID
  Expr.DataElimLitInt = "elim" ID INT
  
  Expr = "(" Expr ")" {bracket}
  
  ID.Syn = RID
  
context-free priorities
  {Expr.TypeLit Expr.TypeLitInt Expr.BoolTrue Expr.BoolFalse Expr.BoolType Expr.BoolIf} > Expr.Var > Expr.FnDestruct > {Expr.AnonFnType Expr.FnType} > Expr.FnConstruct > Expr.TypeAssert > {Expr.Let Expr.Postulate Expr.DataTypeDecl DataConstructor.Constructor}
  
context-free restrictions

  LAYOUT? -/- [\ \n\t\v\f\r]
  
lexical sorts
  RID INT

lexical syntax

  LAYOUT = [\ \n\t\v\f\r]
  
  RID = [a-zA-Z\_] [a-zA-Z0-9\_]*
  RID = "Type" {reject}
  RID = "let" {reject}
  RID = "true" {reject}
  RID = "false" {reject}
  RID = "Bool" {reject}
  RID = "if" {reject}
  RID = "then" {reject}
  RID = "else" {reject}
  RID = "end" {reject}
  RID = "postulate" {reject}
  RID = "elim" {reject}
  
  INT = [0-9]+
  
lexical restrictions

  RID -/- [a-zA-Z0-9\_]
  INT -/- [0-9]


