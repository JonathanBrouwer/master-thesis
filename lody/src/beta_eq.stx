module beta_eq

imports

  signatures/start-sig
  subst
  
signature constructors
  AlphaEqVars : ID * ID -> Expr

rules
  
  expectBetaEq : Expr * Expr
  expectBetaEq(e1, e2) :- expectBetaEq_(betaReduceHead(e1), betaReduceHead(e2)).
  
  expectBetaEq_ : Expr * Expr
  expectBetaEq_(Type(), Type()).
  expectBetaEq_(AlphaEqVars(a, b), AlphaEqVars(a, b)).
  expectBetaEq_(Var(n), Var(n)).
  expectBetaEq_(FnType(arg_name1, arg_type1, body1), FnType(arg_name2, arg_type2, body2)) :-
  	expectBetaEq(arg_type1, arg_type2),
  	expectBetaEq(
  		subst(body1, arg_name1, AlphaEqVars(arg_name1, arg_name2)), 
  		subst(body2, arg_name2, AlphaEqVars(arg_name1, arg_name2))).
  expectBetaEq_(FnConstruct(arg_name1, arg_type1, body1), FnConstruct(arg_name2, arg_type2, body2)) :-
  	expectBetaEq(arg_type1, arg_type2),
  	expectBetaEq(
  		subst(body1, arg_name1, AlphaEqVars(arg_name1, arg_name2)), 
  		subst(body2, arg_name2, AlphaEqVars(arg_name1, arg_name2))).
  expectBetaEq(FnDestruct(fn1, arg1), FnDestruct(fn2, arg2)) :-
  	expectBetaEq(fn1, fn2), expectBetaEq(arg1, arg2).
  
  betaReduceHead : Expr -> Expr
  betaReduceHead(e@Type()) = e.
  betaReduceHead(e@AlphaEqVars(_, _)) = e.
  betaReduceHead(e@Var(_)) = e.
  betaReduceHead(e@FnType(_, _, _)) = e.
  betaReduceHead(e@FnConstruct(_, _, _)) = e.
  
  betaReduceHead(FnDestruct(fn, arg)) = betaReduceHeadFn(FnDestruct(betaReduceHead(fn), arg)).
  betaReduceHeadFn : Expr -> Expr
  betaReduceHeadFn(FnDestruct(FnConstruct(arg_name, arg_type, body), arg)) = betaReduceHead(subst(body, arg_name, arg)).
  betaReduceHeadFn(e@FnDestruct(_, _)) = e.
  
  betaReduceHead(Let(n, v, b)) = betaReduceHead(subst(b, n, v)).
  
  betaReduce : Expr -> Expr
  betaReduce(e) = betaReduce_(betaReduceHead(e)).
  
  betaReduce_ : Expr -> Expr
  betaReduce_(Type()) = Type().
  betaReduce_(Var(id)) = Var(id).
  betaReduce_(FnType(arg_name, arg_type, rtrn)) = FnType(arg_name, betaReduce(arg_type), betaReduce(rtrn)).
  betaReduce_(FnConstruct(arg_name, arg_type, body)) = FnConstruct(arg_name, betaReduce(arg_type), betaReduce(body)).
  betaReduce_(FnDestruct(fn, arg)) = FnDestruct(betaReduce(fn), betaReduce(arg)).
  
  