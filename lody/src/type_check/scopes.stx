module type_check/scopes

imports

  signatures/start-sig
  type_check/type_check
  
signature
sorts
  NameEntry
constructors
  NameType : Expr -> NameEntry
  NameSubst : scope * Expr -> NameEntry
relations
  name : ID -> NameEntry
name-resolution
  labels P
  
rules

  sPutType : scope * ID * Expr -> scope
  sPutType(s, id, t) = s' :-
  	new s', s' -P-> s,
  	!name[id, NameType(t)] in s'.
  	
  sPutSubst : scope * ID * (scope * Expr) -> scope
  sPutSubst(s, id, (sv, v)) = s' :-
  	new s', s' -P-> s,
  	!name[id, NameSubst(sv, v)] in s'.
  	
  sGetName : scope * ID -> (NameEntry * scope)
  sGetName(s, rid) = (result, s') :- {id'}
  	sGetNames(s, rid) == [(_, (id'@ScopedName(s', _), result))] | error $[Unknown variable [rid] in [s]],
  	@rid.ref := id'.

  sGetNames : scope * ID -> list((path * (ID * NameEntry)))
  sGetNames(s, Syn(n)) = results :-
  	query name filter P* and { id' :- rid(id') == n } min $ < P in s |-> results.
  sGetNames(s, id@ScopedName(_, _)) = results :-
  	query name filter P* and { id' :- id' == id } min $ < P in s |-> results.

  sEmpty : -> scope
  sEmpty() = s :- new s.
  
  rid : ID -> RID
  rid(Syn(n)) = n.
  rid(ScopedName(_, n)) = n.

  