module start

context-free start-symbols

  Start

context-free sorts

  Start Stmt Expr

context-free syntax

  Start.Program = Expr
  
  Expr.Let = [let [ID] = [Expr]; [Expr]]
  Expr.Type = "Type"
  Expr.Var = ID
  Expr.FnType = ID ":" Expr "->" Expr {right}
  Expr.FnConstruct = "\\" ID ":" Expr "." Expr
  Expr.FnDestruct = Expr Expr {left}
  Expr.TypeAssert = Expr ":" Expr
  
  Expr.BoolTrue = "true"
  Expr.BoolFalse = "false"
  Expr.BoolType = "Bool"
  Expr.BoolIf = "if" Expr "then" Expr "else" Expr "end"
  
  Expr = "(" Expr ")" {bracket}
  
context-free priorities
  {Expr.Type Expr.BoolTrue Expr.BoolFalse Expr.BoolType Expr.BoolIf} > Expr.Var > Expr.TypeAssert > Expr.FnType > Expr.FnDestruct > Expr.FnConstruct > Expr.Let
  
context-free restrictions

  LAYOUT? -/- [\ \n\t\v\f\r]
  
lexical sorts
  ID

lexical syntax

  LAYOUT = [\ \n\t\v\f\r]
  
  ID = [a-zA-Z\_] [a-zA-Z0-9\_]*
  ID = "Type" {reject}
  ID = "let" {reject}
  ID = "true" {reject}
  ID = "false" {reject}
  ID = "Bool" {reject}
  ID = "if" {reject}
  ID = "then" {reject}
  ID = "else" {reject}
  ID = "end" {reject}
  
lexical restrictions

  ID -/- [a-zA-Z0-9\_]


