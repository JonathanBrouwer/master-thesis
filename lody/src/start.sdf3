module start

context-free start-symbols

  Start

context-free sorts

  Start Stmt Expr ID

context-free syntax

  Start.Program = Expr
  
  Expr.Let = [let [ID] = [Expr]; [Expr]]
  Expr.Type = "Type"
  Expr.Var = ID
  Expr.FnType = ID ":" Expr "->" Expr {right}
  Expr.FnConstruct = "\\" ID ":" Expr "." Expr
  Expr.FnDestruct = Expr Expr {left}
  Expr.TypeAssert = Expr ":" Expr
  
  Expr.BoolTrue = "true"
  Expr.BoolFalse = "false"
  Expr.BoolType = "Bool"
  Expr.BoolIf = "if" Expr "then" Expr "else" Expr "end"
  
  ID = IDR
  
  Expr = "(" Expr ")" {bracket}
  
context-free priorities
  {Expr.Type Expr.BoolTrue Expr.BoolFalse Expr.BoolType Expr.BoolIf} > Expr.Var > Expr.TypeAssert > Expr.FnType > Expr.FnDestruct > Expr.FnConstruct > Expr.Let
  
context-free restrictions

  LAYOUT? -/- [\ \n\t\v\f\r]
  
lexical sorts
  IDR

lexical syntax

  LAYOUT = [\ \n\t\v\f\r]
  
  IDR = [a-zA-Z\_] [a-zA-Z0-9\_]*
  IDR = "Type" {reject}
  IDR = "let" {reject}
  IDR = "true" {reject}
  IDR = "false" {reject}
  IDR = "Bool" {reject}
  IDR = "if" {reject}
  IDR = "then" {reject}
  IDR = "else" {reject}
  IDR = "end" {reject}
  
lexical restrictions

  IDR -/- [a-zA-Z0-9\_]


